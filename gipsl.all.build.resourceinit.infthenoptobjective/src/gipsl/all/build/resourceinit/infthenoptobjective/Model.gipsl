package "gipsl.all.build.resourceinit.infthenoptobjective"
import "platform:/resource/gipsl.all.build.model/model/Model.ecore"

config {  
	solver := GUROBI;
	timeLimit := true [value := 10.0];
	randomSeed := true [value := 0];
	presolve := true;
	debugOutput := false;
}

rule mapVnode {
	root: Root {
		-containers -> substrateContainer
		-containers -> virtualContainer
	}
	
	substrateContainer: SubstrateContainer {
		-substrateNodes -> snode
	}
	
	virtualContainer: VirtualContainer {
		-virtualNodes -> vnode
	}
	
	snode: SubstrateResourceNode
	
	vnode: VirtualResourceNode {
		++ -host -> snode
	}
}

//
// GIPSL starts here!
//

mapping n2n to mapVnode;

// Exactly one mapping per virtual resource node
constraint with VirtualResourceNode {
	mappings.n2n->filter(element.nodes.vnode == context)->sum(element.value) == 1
}

// Do not overbook substrate resource nodes
constraint with SubstrateResourceNode {
	mappings.n2n->filter(element.nodes.snode == context)->sum(element.nodes.vnode.resourceDemand * element.value) <= context.resourceAmountAvailable
}

function nObj with n2n {
	context.nodes.vnode.resourceDemand * context.value
}

objective : max {
	2 * functions.nObj
}
