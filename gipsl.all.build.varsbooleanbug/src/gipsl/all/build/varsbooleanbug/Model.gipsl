package "gipsl.all.build.varsbooleanbug"
import "platform:/resource/gipsl.all.build.model/model/Model.ecore"
import "http://www.eclipse.org/emf/2002/Ecore"

config {  
	solver := GUROBI;
	timeLimit := true [value := 10.0];
	randomSeed := true [value := 0];
	presolve := true;
	debugOutput := false;
}

rule mapVnode {
	root: Root {
		-containers -> substrateContainer
		-containers -> virtualContainer
	}
	
	substrateContainer: SubstrateContainer {
		-substrateNodes -> snode
	}
	
	virtualContainer: VirtualContainer {
		-virtualNodes -> vnode
	}
	
	snode: SubstrateResourceNode
	
	vnode: VirtualResourceNode {
		++ -host -> snode
	}
}

rule setDummyAttrRoot(p: EBoolean) {
	root: Root {
		.dummyAttribute := true
	}
	// the parameter `p` must not be used for this test
}

//
// GIPSL starts here!
//

mapping n2n with mapVnode {
	var b : EBoolean
};

mapping dummyM with setDummyAttrRoot {
	var x : EBoolean bind p
};

constraint -> class::VirtualResourceNode {
	mappings.n2n->filter(m | m.nodes().vnode == self)->count() == 1
}

constraint -> class::Root {
	mappings.dummyM->filter(m | m.nodes().root == self)->count() == 1
}

// Sets the respective variable `b` to `1` if the corresponding mapping has been chosen
constraint -> pattern::mapVnode {
	mappings.n2n->filter(m | m.nodes().snode == self.nodes().snode & m.nodes().vnode == self.nodes().vnode)->count()
	==
	mappings.n2n->filter(m | m.nodes().snode == self.nodes().snode & m.nodes().vnode == self.nodes().vnode)->sum(m | m.variables().b)
}

// Sets the respective variable `p` to `1` if the corresponding mapping has been chosen
constraint -> pattern::setDummyAttrRoot {
	mappings.dummyM->filter(m | m.nodes().root == self.nodes().root)->count()
	==
	mappings.dummyM->filter(m | m.nodes().root == self.nodes().root)->sum(m | m.variables().x)
}

objective n2nObj -> mapping::n2n {
	1
}

global objective : max {
	n2nObj
}
