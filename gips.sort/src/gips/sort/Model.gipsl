package "gips.sort"
import "platform:/resource/gips.sort.listmodel/model/Listmodel.ecore"

config {  
	solver := GUROBI;
	timeLimit := true [value := 42.0];
	randomSeed := true [value := 73];
	presolve := true;
	debugOutput := false;
}

rule sortOne {
	root: Root {
		-entries -> valA
		-entries -> valB
	}
	
	valA: Entry {
		++ -next -> valB
	}
	
	valB: Entry
		
	# valA.^value < valB.^value
}

rule sortLast {
	root: Root {
		-entries -> val
	}
	
	val: Entry {
		++ -next -> val
	}
}

mapping e2e to sortOne;
mapping e2self to sortLast;

constraint with Entry {
	mappings.e2e->filter(element.nodes.valA == context)->count() + mappings.e2self->filter(element.nodes.val == context)->count() == 1
}

constraint with Entry {
	mappings.e2e->filter(element.nodes.valB == context)->count() <= 1
} 

constraint {
	mappings.e2self->count() == 1
}
