package "shortestpath"
import "platform:/resource/genericgraphmetamodel/model/Genericgraphmetamodel.ecore"

// This specification solves the "shortest path" problem (which, normally,
// could be solved by the algorithm of Dijkstra).
//
// Assumptions:
// - the graph is connected
// - there is exactly one source node, i.e., there is exactly one node
//   that does not have any incoming edges
// - there is exactly one target node, i.e., there is exactly one node
//   that does not have any outgoing edges
// - the graph does not contain any cycles
//
// You should not solve a standard shortest path problem with this
// implementation. It was developed solely for testing purposes.

config {  
	solver := GUROBI;
	timeLimit := true [value := 10.0];
	randomSeed := true [value := 0];
	presolve := true;
	debugOutput := false;
	tolerance := true [value := 0.00001];
	lpOutput := false [path := "./gurobi.lp"];
}

rule selectEdge {
	source : Node
	target : Node
	edge : Edge {
		- source -> source
		- target -> target
		.selected := true
	}
}

pattern findSourceNode {
	node : Node
	root : Root {
		- sourceNode -> node
	}
}

pattern findTargetNode {
	node : Node
	root : Root {
		- targetNode -> node
	}
}

condition isMiddleNode = forbid findSourceNode && forbid findTargetNode
pattern findMiddleNode {
	node : Node
}
when isMiddleNode



//
// GIPSL
//

mapping selectEdge with selectEdge;

constraint -> pattern::findSourceNode {
	mappings.selectEdge->filter(m | m.nodes().source == self.nodes().node)->count() == 1
	&
	mappings.selectEdge->filter(m | m.nodes().target == self.nodes().node)->count() == 0
}

constraint -> pattern::findTargetNode {
	mappings.selectEdge->filter(m | m.nodes().target == self.nodes().node)->count() == 1
	&
	mappings.selectEdge->filter(m | m.nodes().source == self.nodes().node)->count() == 0
}

constraint -> pattern::findMiddleNode {
	mappings.selectEdge->filter(m | m.nodes().target == self.nodes().node)->count() == 1
	<=>
	mappings.selectEdge->filter(m | m.nodes().source == self.nodes().node)->count() == 1
}

constraint -> class::Node {
	mappings.selectEdge->filter(m | m.nodes().target == self)->count() <= 1
	&
	mappings.selectEdge->filter(m | m.nodes().source == self)->count() <= 1
}

// Local objective is the weight of the selected edges
objective o -> mapping::selectEdge {
	self.nodes().edge.weight
}

// Global objective should be minimized
global objective : min {
	o
}
